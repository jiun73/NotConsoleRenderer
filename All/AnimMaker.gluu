new vector(AnimationX) animations
new vector(string) animation_names
new vector(string) paths
new int current_frame = 0
new int current_animation = 0
new int current_image = 0

new function :curanim = 
{
	animations -at current_animation
}

new function :curframe = 
{
	:curanim .frames -at current_frame
}

new init setup = { BASE .fit = :true; }

new function :load_images = 
{
	vector(string) files = $file;
	int index = 0;

	$while { index /- -size files } {
		files -at index :get_tex;
		paths -push {files -at index};
		index ++;
	};
}

new function :has_image = { paths -size /+ 0  }
new function :has_anim = { animations -size /+ 0 }
new function :animation_size =  { :curanim .frames -size }

new function :has_frames = 
{
	$if :has_anim  
	{
		return { :curanim .frames -size /+ 0 } 
	};
	:false;
}

new function :has_textures = 
{
	$if :has_anim 
	{
		return { :curanim .textures -size /+ 0 }
	};
	:false;
}

new function :add_frame =
{
	arg string path;
	arg* AnimationX anim;

	$if { @ anim .textures -has :get_tex path :not }
	{ 
		 @ anim .textures -push :get_tex path ;
	};

	int pos = { @ anim .textures -find :get_tex path };

	AnimationFrameX frame;

	frame .tex = (size_t) pos;
	frame .time = (u32) 100;

	@ anim .frames -push frame;
}

new function :set_origins_ratio = {
	arg double ratioY;
	arg double ratioX;

	int i = 0;

	$foreach { :curanim .frames }
	{
		V2d_i tex_size = { :curanim .textures -at (size_t-int) { :curanim .frames -at i .tex } :get_tex_sz };

		:curanim .frames -at i .origin .x = {tex_size .x (.) !* ratioX (~)};
		:curanim .frames -at i .origin .y = {tex_size .y (.) !* ratioY (~)};

		i ++;
	};
}

ROW SIZE 30 CFIT <
	COL SIZE 120 BUTTON { "Load images" } { :load_images } <>
	COL SIZE 900 DROPBOX {paths} {current_image} <>
	//COL SIZE 120 IF { :has_anim } 
	//BUTTON { "Add image" } 
	//{
	//	{ {animations -at current_animation} .textures} -push {:get_tex paths -at current_image};
	//	:cout {"Image added (" { {animations -at current_animation} .textures -size } ")"};
	//} <>
>

ROW SIZE 30 <
	new bool makingAnim = false 

	COL SIZE 140 IF { :not makingAnim }	BUTTON	{"New Animation"}	{ makingAnim = :true } <>
	COL SIZE 170 IF { :not makingAnim } DROPBOX {animation_names} {current_animation} <>
	COL SIZE 140 IF { makingAnim }		TEXTBOX {"Enter name..."}	
	{ 
		arg* string text; 

		AnimationX new_anim;
		animation_names -push @ text;
		animations -push new_anim;

		:cout { "Added " @ text " animation" };

		makingAnim = :false; 
		@ text = ""; 
	} 
	<>
	COL SIZE 140 IF { makingAnim }		BUTTON	{"Cancel"}			{ makingAnim = :false } <>

	COL SIZE 150 IF { :has_anim :and :has_image } BUTTON { "Add frame" } 
			{
				& :curanim :add_frame { paths -at current_image} ;
			} <>

	COL SIZE 150 IF { :has_anim :and :has_image } BUTTON { "Add all images" } 
	{
		new int i;
		i = 0;
		$foreach paths 
		{
			& :curanim :add_frame { paths -at i };
			i ++;
		};
		
	} <>

	COL CFIT <
		ROW SIZE 30 IF { :has_frames } <
			COL SIZE 70 TEXT { current_frame " / " {:animation_size - 1} } <>

			COL SIZE 40 BUTTON { "Prev" }  
			{
				$if {current_frame /+ 0} {current_frame --}
			} <>

			COL SIZE 40 BUTTON { "Next" }  
			{
				$if {current_frame /-  {:animation_size - 1} } 
				{current_frame ++}
			} <>
		>
	>
>

new bool state = false

ROW SIZE 800 CFIT  <
	COL SIZE 4 IF { :has_frames } FRAME_CREATOR { :curanim } { :curframe } <>

	

	COL CFIT SIZE 1 CFIT IF { :has_frames } 
	<
		ROW ANIMATION_PREVIEW { :curanim } <>
		ROW CFIT < COL < 
			ROW SIZE 30 TEXT { "Size: " {:get_tex_sz { :curanim .textures -at (size_t-int) {:curframe .tex} } } } <>
			ROW SIZE 30 TEXTBOX { "Origin: " :curframe .origin } { 
				arg* string text; 
				:curframe .origin #= @ text;
				@ text = ""; 
			} <> 
			ROW SIZE 30 TEXTBOX { "Speed: " :curframe .time } { 
				arg* string text; 
				:curframe .time #= @ text;
				@ text  = ""; 
			} <>
			ROW SIZE 30 IF { :not state } BUTTON { "More... " } { state = :true; } <>
			ROW SIZE 30 IF { state } BUTTON { "Close" } { state = :false; } <>
			ROW SIZE 30 IF { state } BUTTON { "Copy settings to all frames" } 
			{
				int i = 0;
				$foreach { :curanim .frames }
				{
					:curanim .frames -at i .origin = { :curframe .origin } ;
					i ++;
				};
			} <>

			new double ZERO = 0
			new double HALF = 0.5
			new double ONE = 1

			ROW SIZE 30 CFIT IF { state }
			<
				COL BUTTON { "*" } { :set_origins_ratio ZERO ZERO } <>
				COL BUTTON { "*" } { :set_origins_ratio HALF ZERO } <>
				COL BUTTON { "*" } { :set_origins_ratio ONE ZERO } <>
			>

			ROW SIZE 30 CFIT IF { state }
			<
				COL BUTTON { "*" } { :set_origins_ratio ZERO HALF } <>
				COL BUTTON { "*" } { :set_origins_ratio HALF HALF } <>
				COL BUTTON { "*" } { :set_origins_ratio ONE HALF } <>
			>

			ROW SIZE 30 CFIT IF { state }
			<
				COL BUTTON { "*" } { :set_origins_ratio ZERO ONE } <>
				COL BUTTON { "*" } { :set_origins_ratio HALF ONE } <>
				COL BUTTON { "*" } { :set_origins_ratio ONE ONE } <>
			>
		>>

	>
>

